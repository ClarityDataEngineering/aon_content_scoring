config {
    type: 'view',
    schema: project_config.OUTPUT_STAGING_DATASET, 
    description: 'Weekly GSC impressions per content page with 12-month rolling total, months live, monthly average, and weekly relative percentile-based scoring. For each week and page, assigns a score from 1 (bottom 40%) to 5 (top 20%) based on the distribution of monthly average impressions among all pages that week.',
    bigquery: {
        labels: {
          table_name: "content_scoring_stg_metric_gsc_impressions"
        }
    }
}


-- Get base impressions and calculate at weekly cadence
with base_impressions as (
    select
        date_trunc(date, week(MONDAY)) as event_date_dt
        , page_location_clean
        , sum(impressions) as impressions
    from 
        ${ref('fct_gsc_url_report')}
    where
        regexp_contains(page_location_clean, r'${project_config.PAGES_TO_INCLUDE_RX}')
        and date >= "${project_config.START_DATE}"
    group by 
        event_date_dt
        , page_location_clean
)

-- Join with weekly scaffold 
, impressions_live_pages as (
    select
        sc.event_date_dt
        , sc.page_location_clean
        , coalesce(bs.impressions, 0) as impressions
        , sc.first_seen_date
        , sc.months_live
    from 
        ${ref('stg_ga4_page_live_weekly_scaffold')} sc
    left join 
        base_impressions bs
        using (event_date_dt, page_location_clean)
    where
        sc.is_page_live_week = true  -- Only include pages where the page is live in a given week
) 

-- Use window function to calculate a 12 month rolling looking back over last 52 rows
, rolling_12_month as (
    select
        event_date_dt
        , page_location_clean
        , first_seen_date
        , months_live
        , sum(impressions) over (partition by page_location_clean order by event_date_dt rows between 51 preceding and current row) as total_12_months
    from 
        impressions_live_pages
)

-- Join the months live and calculate the monthly avg. Add filter to include rows only post first seen date
, calc_monthly_avg as (
    select
        event_date_dt
        , page_location_clean
        , total_12_months
        , months_live
        , first_seen_date
        , cast(round(safe_divide(total_12_months, months_live)) as int64) as monthly_avg
    from 
        rolling_12_month
    where
        event_date_dt >= first_seen_date
)

-- Calculate the percentiles at a weekly cadence 
, weekly_percentiles as (
    select
        event_date_dt
        , page_location_clean
        , total_12_months
        , months_live
        , first_seen_date
        , monthly_avg
        , cast(round(percentile_cont(monthly_avg, 0.9) over(partition by event_date_dt)) as int64) as percentile_90
        , cast(round(percentile_cont(monthly_avg, 0.8) over(partition by event_date_dt)) as int64) as percentile_80
        , cast(round(percentile_cont(monthly_avg, 0.7) over(partition by event_date_dt)) as int64) as percentile_70
        , cast(round(percentile_cont(monthly_avg, 0.5) over(partition by event_date_dt)) as int64) as percentile_50
    from 
        calc_monthly_avg
)

-- Add case for impression percentile scoring logic
select
    event_date_dt
    , page_location_clean
    , total_12_months
    , months_live
    , first_seen_date
    , monthly_avg
    , percentile_90
    , percentile_80
    , percentile_70
    , percentile_50
    , case

        when monthly_avg >= percentile_90 then 5
        when monthly_avg >= percentile_80 and monthly_avg < percentile_90 then 4
        when monthly_avg >= percentile_70 and monthly_avg < percentile_80 then 3
        when monthly_avg >= percentile_50 and monthly_avg < percentile_70 then 2
        when monthly_avg < percentile_50 then 1
    end as score_gsc_impressions
from 
    weekly_percentiles

