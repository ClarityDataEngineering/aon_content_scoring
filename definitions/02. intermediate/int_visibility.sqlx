config {
    type: 'view',
    schema: project_config.OUTPUT_STAGING_DATASET, 
    description: 'Perform joins of Visibility page metrics',
    bigquery: {
        labels: {
          table_name: "content_scoring_int_visibility"
        }
    }
}
-- Join all the visibility metrics together 
select
    wps.event_date_dt
    , wps.page_location_clean
    , mrs.score_ga4_referral_traffic
    , clk.score_gsc_clicks
    , imp.score_gsc_impressions
    , ctr.score_gsc_ctr
    , brq.score_gsc_branded_queries
    , rch.score_org_social_reach
    , obk.score_org_rank_backlinks
    , ork.score_org_rank_rankings
    , ord.score_org_rank_ref_domain_authority
from 
    ${ref('stg_ga4_page_live_weekly_scaffold')} wps
left join ${ref('stg_metric_ga4_referral_sessions')} mrs
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_gsc_clicks')} clk
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_gsc_impressions')} imp
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_gsc_branded_queries')} brq
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_gsc_ctr')} ctr
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_org_social_post_reach')} rch
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_org_rank_backlinks')} obk
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_org_rank_rankings')} ork
    using(event_date_dt, page_location_clean)
left join ${ref('stg_metric_org_rank_ref_domain_authority')} ord
    using(event_date_dt, page_location_clean)

where
    wps.is_page_live_week = true
    

    