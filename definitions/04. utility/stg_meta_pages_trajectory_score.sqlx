config {
    type: 'view',
    schema: project_config.OUTPUT_STAGING_DATASET, 
    description: 'Weekly rolling trajectory score calculation for content pages based on growth velocity and performance consistency. Trajectory Score = (Growth Velocity × 0.5) + (1/Standard Deviation × 0.5). Calculates 12-month rolling pageview totals with 4-week lag for month-over-month growth approximation. Pages in their first 4 weeks receive a score of 0 due to insufficient historical data for meaningful trajectory.',
    bigquery: {
        labels: {
          table_name: "content_scoring_stg_meta_page_trajectory_score"
        }
    }
}

-- Get base unique pageviews at weekly cadence
with base_unique_pv as (
    select
        date_trunc(event_date_dt, week(MONDAY)) as event_date_dt
        , page_location_clean
        , sum(unique_page_views) as unique_pageviews
    from 
        ${ref('final_ga4_pages')}
    where
        regexp_contains(page_location_clean, r'${project_config.PAGES_TO_INCLUDE_RX}')
        and event_date_dt >= "${project_config.START_DATE}"
    group by 
        event_date_dt
        , page_location_clean
)

-- Join with weekly scaffold - 
, unique_pv_live_pages as (
    select
        sc.event_date_dt
        , sc.page_location_clean
        , coalesce(bs.unique_pageviews, 0) as unique_pageviews
        , sc.first_seen_date
        , sc.months_live
        , sc.weeks_live
    from 
        ${ref('stg_ga4_page_live_weekly_scaffold')} sc
    left join 
        base_unique_pv bs
        using (event_date_dt, page_location_clean)
    where
        sc.is_page_live_week = true  -- Only include pages where the page is live in a given week
)

-- Calculate 12-month rolling sum
, rolling_12_month as (
    select
        event_date_dt
        , page_location_clean
        , first_seen_date
        , months_live
        , weeks_live
        , sum(unique_pageviews) over (
            partition by page_location_clean 
            order by event_date_dt 
            rows between 51 preceding and current row
        ) as total_12_months
    from 
        unique_pv_live_pages
)

-- Add lag for growth calculation
, rolling_12_month_with_lag as (
    select
        event_date_dt
        , page_location_clean
        , first_seen_date
        , months_live
        , weeks_live
        , total_12_months
        , lag(total_12_months, 4) over (
            partition by page_location_clean 
            order by event_date_dt
        ) as total_12_months_4_weeks_ago
    from 
        rolling_12_month
)

, trajectory_components as (
    select
        event_date_dt
        , page_location_clean
        , first_seen_date
        , months_live
        , weeks_live
        , total_12_months
        , total_12_months_4_weeks_ago
        -- Growth calculation (4-week growth to approximate monthly)
        , case 
            when weeks_live <= 4 then 0 -- First 4 weeks always 0
            when total_12_months_4_weeks_ago > 0 and months_live >= 1
            then (total_12_months - total_12_months_4_weeks_ago) / greatest(months_live, 1)
            else 0 
          end as growth_velocity
        -- Calculate rolling standard deviation of weekly pageviews over past 12 months
        , case 
            when weeks_live <= 4 then 0  -- First 4 weeks always 0
            else stddev(total_12_months) over (
                partition by page_location_clean 
                order by event_date_dt 
                rows between 51 preceding and current row
            )
          end as pageviews_stddev
    from 
        rolling_12_month_with_lag
)
-- Calculate trajectory score
, calculate_trajectory_score as (
    select
        event_date_dt
        , page_location_clean
        , first_seen_date
        , months_live
        , weeks_live
        , total_12_months
        , total_12_months_4_weeks_ago
        , growth_velocity
        , pageviews_stddev
        , case 
            when weeks_live <= 4 then 0.00  -- First 4 weeks always 0
            when pageviews_stddev > 0 
            then round((growth_velocity * 0.5) + ((1.0 / pageviews_stddev) * 0.5), 2)
            else 0.00
            end as trajectory_score
    from 
        trajectory_components
)
-- Calculate the weekly percentiles for trajectory score
, weekly_percentiles as (
    select
        event_date_dt
        , page_location_clean
        , first_seen_date
        , months_live
        , weeks_live
        , total_12_months
        , total_12_months_4_weeks_ago
        , growth_velocity
        , pageviews_stddev
        , trajectory_score
        , cast(round(percentile_cont(trajectory_score, 0.90) over(partition by event_date_dt)) as int64) as percentile_90
        , cast(round(percentile_cont(trajectory_score, 0.70) over(partition by event_date_dt)) as int64) as percentile_70
        , cast(round(percentile_cont(trajectory_score, 0.50) over(partition by event_date_dt)) as int64) as percentile_50
        , cast(round(percentile_cont(trajectory_score, 0.30) over(partition by event_date_dt)) as int64) as percentile_30
    from 
        calculate_trajectory_score
)
-- Final output and logic for trajectory scoring based on the percentile
select
    event_date_dt
    , page_location_clean
    , first_seen_date
    , months_live
    , weeks_live
    , total_12_months
    , total_12_months_4_weeks_ago
    , growth_velocity
    , pageviews_stddev
    , trajectory_score
    , percentile_90
    , percentile_70
    , percentile_50
    , percentile_30
    , case 
        when trajectory_score >= percentile_90 then 5
        when trajectory_score >= percentile_70 and trajectory_score < percentile_90 then 4
        when trajectory_score >= percentile_50 and trajectory_score < percentile_70 then 3
        when trajectory_score >= percentile_30 and trajectory_score < percentile_50 then 2
        when trajectory_score < percentile_30 then 1
    end as score_trajectory_percentile
from 
    weekly_percentiles
